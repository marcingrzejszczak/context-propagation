def VERSIONS = [
		'org.reactivestreams:reactive-streams:1.0.3',
		'io.projectreactor:reactor-core:3.4.12',
		// logging
		'ch.qos.logback:logback-classic:1.2.+',
		'org.apache.logging.log4j:log4j-core:2.+',
		'org.slf4j:slf4j-api:1.7.+',
		// test
		'org.assertj:assertj-core:latest.release',
		'org.awaitility:awaitility:latest.release',
		'org.junit.jupiter:junit-jupiter:5.8.+',
		'org.junit.platform:junit-platform-launcher:1.8.+',
		'org.junit.vintage:junit-vintage-engine:5.8.+',
		'org.mockito:mockito-core:latest.release',
		'org.mockito:mockito-inline:latest.release'
]

subprojects {
	plugins.withId('java-library') {
		dependencies {
			constraints {
				// Direct dependencies
				VERSIONS.each { version ->
					// java-library plugin has three root configurations, so we apply constraints too all of
					// them so they all can use the managed versions.
					api version
					compileOnly version
					runtimeOnly version
				}
			}
		}
	}
}
